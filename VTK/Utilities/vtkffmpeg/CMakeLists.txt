PROJECT(VTKFFMPEG)

INCLUDE(DetectSystemFeatures.cmake)

IF(WIN32)
  IF(BUILD_SHARED_LIBS)
    SET(FFMPEG_DLL 1)
  ENDIF(BUILD_SHARED_LIBS)
ENDIF(WIN32)

CONFIGURE_FILE(config.h.in ${CMAKE_CURRENT_BINARY_DIR}/config.h @ONLY)

INCLUDE_DIRECTORIES(BEFORE libavutil libavcodec libavformat
  ${CMAKE_CURRENT_BINARY_DIR} ${MALLOC_H_DIR})

ADD_DEFINITIONS(-DHAVE_AV_CONFIG_H -D_FILE_OFFSET_BITS=64
  -D_LARGEFILE_SOURCE -D_ISOC9X_SOURCE -DBUILD_AVUTIL)

# source files for ffmpeg
INCLUDE(libavutil/files.cmake)
INCLUDE(libavcodec/files.cmake)
INCLUDE(libavformat/files.cmake)

# set all sources (even the .S files, they have to be preprocessed) to be C
SET_SOURCE_FILES_PROPERTIES(
  ${FFMPEG_avutil_SRCS}
  ${FFMPEG_avcodec_SRCS}
  ${FFMPEG_avformat_SRCS}
  PROPERTIES LANGUAGE C
  )

CONFIGURE_FILE(${VTKFFMPEG_SOURCE_DIR}/.NoDartCoverage
               ${VTKFFMPEG_BINARY_DIR}/.NoDartCoverage)
CONFIGURE_FILE(${VTKFFMPEG_SOURCE_DIR}/ffmpegDllConfig.h.in
               ${VTKFFMPEG_BINARY_DIR}/ffmpegDllConfig.h)

ADD_LIBRARY(vtkffmpeg SHARED
  ${FFMPEG_avutil_SRCS}
  ${FFMPEG_avcodec_SRCS}
  ${FFMPEG_avformat_SRCS}
  )

IF(VTK_FFMPEG_ALTIVEC_FLAGS)
  SET_TARGET_PROPERTIES(vtkffmpeg PROPERTIES
    COMPILE_FLAGS ${VTK_FFMPEG_ALTIVEC_FLAGS}
    )
ENDIF(VTK_FFMPEG_ALTIVEC_FLAGS)

IF(APPLE)
  SET_TARGET_PROPERTIES(vtkffmpeg PROPERTIES
    LINK_FLAGS "-Wl,-dynamic,-search_paths_first,-single_module"
    )
ENDIF(APPLE)

# Apply user-defined properties to the library target.
IF(VTK_LIBRARY_PROPERTIES)
  SET_TARGET_PROPERTIES(vtkffmpeg PROPERTIES ${VTK_LIBRARY_PROPERTIES})
ENDIF(VTK_LIBRARY_PROPERTIES)

IF(NOT VTK_INSTALL_NO_LIBRARIES)
  INSTALL(TARGETS vtkffmpeg
    RUNTIME DESTINATION ${VTK_INSTALL_BIN_DIR_CM24} COMPONENT RuntimeLibraries
    LIBRARY DESTINATION ${VTK_INSTALL_LIB_DIR_CM24} COMPONENT RuntimeLibraries
    ARCHIVE DESTINATION ${VTK_INSTALL_LIB_DIR_CM24} COMPONENT Development)
ENDIF(NOT VTK_INSTALL_NO_LIBRARIES)
