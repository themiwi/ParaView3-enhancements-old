IF(BUILD_TYPE STREQUAL "Debug")
  SET(OVERVIEW_BINARY_LOCATIONS "@OVERVIEW_DEBUG_BINARY_LOCATIONS@")
ELSEIF(BUILD_TYPE STREQUAL "Release")
  SET(OVERVIEW_BINARY_LOCATIONS "@OVERVIEW_RELEASE_BINARY_LOCATIONS@")
ELSE()
  MESSAGE(ERROR "Unknown build type: ${BUILD_TYPE}")
ENDIF()

SET(SKIP_DEPENDENCIES
  "^C:/WINDOWS/system32"
  "^gdiplus.dll$"
  "^msjava.dll$"
  )

INCLUDE(GetPrerequisites)

FOREACH(LOCATION ${OVERVIEW_BINARY_LOCATIONS})
  MESSAGE(STATUS "Installing: ${LOCATION} dependencies")

  GET_PREREQUISITES(${LOCATION} DEPENDENCIES 1 1 "" "")

  FOREACH(DEPENDENCY ${DEPENDENCIES})
    SET(INSTALL_DEPENDENCY 1)

    GP_RESOLVE_ITEM("${LOCATION}" "${DEPENDENCY}" "" "" DEPENDENCY)

    FOREACH(SKIP_DEPENDENCY ${SKIP_DEPENDENCIES})
      IF(DEPENDENCY MATCHES "${SKIP_DEPENDENCY}")
        SET(INSTALL_DEPENDENCY 0)
      ENDIF()
    ENDFOREACH()

    IF(INSTALL_DEPENDENCY)
      FILE(INSTALL
        DESTINATION "${CMAKE_INSTALL_PREFIX}/bin"
        TYPE EXECUTABLE
        FILES "${DEPENDENCY}"
        )
    ENDIF()
  ENDFOREACH()
ENDFOREACH()

