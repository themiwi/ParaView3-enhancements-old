PROJECT(OverView)

CMAKE_MINIMUM_REQUIRED(VERSION 2.6 FATAL_ERROR)

# Set global configuration options ...
SET(OVERVIEW_VERSION_MAJOR 0)
SET(OVERVIEW_VERSION_MINOR 2)
SET(OVERVIEW_VERSION_PATCH 1)
SET(OVERVIEW_VERSION_TYPE "(alpha)")
SET(OVERVIEW_VERSION "${OVERVIEW_VERSION_MAJOR}.${OVERVIEW_VERSION_MINOR}.${OVERVIEW_VERSION_PATCH}")
SET(OVERVIEW_VERSION_FULL "${OVERVIEW_VERSION} ${OVERVIEW_VERSION_TYPE}")
SET(OVERVIEW_GEO_TILE_PATH "" CACHE PATH "Tile database path")

# Prompt the user for optional functionality ...
#OPTION(OVERVIEW_INSTALLER_SUPPORT "Include support for packaging OverView into a binary installer" OFF)

# Setup branding ...
SET(OVERVIEW_BRANDING_CONFIGURATION "${CMAKE_CURRENT_SOURCE_DIR}/Branding/OverView/Configuration.cmake" CACHE FILEPATH "Branding configuration file")
INCLUDE(${OVERVIEW_BRANDING_CONFIGURATION})

SET(OVERVIEW_BRANDED_VERSION "${OVERVIEW_BRANDED_VERSION_MAJOR}.${OVERVIEW_BRANDED_VERSION_MINOR}.${OVERVIEW_BRANDED_VERSION_PATCH}")
SET(OVERVIEW_BRANDED_VERSION_FULL "${OVERVIEW_BRANDED_VERSION} ${OVERVIEW_BRANDED_VERSION_TYPE}")

IF(WIN32)
  CONFIGURE_FILE(${OVERVIEW_BRANDED_APPLICATION_ICON} ${CMAKE_CURRENT_BINARY_DIR}/Icon.ico COPYONLY)
ENDIF(WIN32)

# Do sanity-checking on the current configuration ...
SET(QT_USE_QTASSISTANT TRUE)
SET(QT_USE_QTNETWORK TRUE) # QtAssistant depends on it (only for linking)
SET(QT_USE_QTSQL TRUE)
SET(QT_USE_QTUITOOLS TRUE)
INCLUDE(${QT_USE_FILE})

# Build the OverView client ...
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/Config.h.in ${CMAKE_CURRENT_BINARY_DIR}/Config.h)
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/Icon.rc.in ${CMAKE_CURRENT_BINARY_DIR}/Icon.rc)
CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/SplashScreen.qrc.in ${CMAKE_CURRENT_BINARY_DIR}/SplashScreen.qrc)
CONFIGURE_FILE(${OVERVIEW_BRANDED_SPLASH_IMAGE} ${CMAKE_CURRENT_BINARY_DIR}/SplashScreen COPYONLY)

QT4_WRAP_CPP(MOC_BUILT_SOURCES
  AboutDialog.h
  ApplicationOptionsDialog.h
  DisplayPolicy.h
  GlobalGraphViewOptions.h
  MainWindow.h
)

SET(UI_FORMS
  AboutDialog.ui
  GlobalGraphViewOptions.ui
  MainWindow.ui
)

QT4_WRAP_UI(UI_BUILT_SOURCES
  ${UI_FORMS}
)

SET(UI_RESOURCES
  ${CMAKE_CURRENT_BINARY_DIR}/SplashScreen.qrc
)

QT4_ADD_RESOURCES(RCS_SOURCES
  ${UI_RESOURCES}
)

IF(WIN32)
  SET(EXE_ICON ${CMAKE_CURRENT_BINARY_DIR}/Icon.rc)
ENDIF(WIN32)

SOURCE_GROUP("Resources" FILES
  ${UI_RESOURCES}
  ${UI_FORMS}
  ${EXE_ICON}
)

SOURCE_GROUP("Generated" FILES
  ${MOC_BUILT_SOURCES}
  ${RCS_SOURCES}
  ${UI_BUILT_SOURCES}
)

IF(WIN32)
  LINK_DIRECTORIES(${QT_LIBRARY_DIR})
ENDIF(WIN32)

INCLUDE_DIRECTORIES(
  ${CMAKE_CURRENT_BINARY_DIR}
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${OverViewUtility_BINARY_DIR}
  ${OverViewUtility_SOURCE_DIR}
  )

ADD_EXECUTABLE(${OVERVIEW_BRANDED_APPLICATION_TITLE} WIN32 MACOSX_BUNDLE
  AboutDialog.cxx
  AboutDialog.h
  ApplicationOptionsDialog.cxx
  ApplicationOptionsDialog.h
  DisplayPolicy.cxx
  DisplayPolicy.h
  GlobalGraphViewOptions.cxx
  GlobalGraphViewOptions.h
  MainWindow.cxx
  MainWindow.h
  main.cxx
  ProcessModuleGUIHelper.cxx
  ProcessModuleGUIHelper.h
  ${MOC_BUILT_SOURCES}
  ${RCS_SOURCES}
  ${UI_BUILT_SOURCES}
  ${EXE_ICON}
)

TARGET_LINK_LIBRARIES(${OVERVIEW_BRANDED_APPLICATION_TITLE}
  pqComponents
  pqCore
  pqWidgets
  QtChart
  QtTesting
  ${QT_LIBRARIES}
  OverViewUtility
)

IF(WIN32)
  TARGET_LINK_LIBRARIES(${OVERVIEW_BRANDED_APPLICATION_TITLE} ${QT_QTMAIN_LIBRARY} )
ENDIF(WIN32)

#INSTALL(
# TARGETS ${OVERVIEW_BRANDED_APPLICATION_TITLE}
# DESTINATION bin
# COMPONENT OverView
# )

# Install shared data
#INSTALL(FILES
# ${VTKSNL_DATA_DIRECTORY}/Applications/GeoView/NE2_ps_bath.png
# ${VTKSNL_DATA_DIRECTORY}/Applications/GeoView/political.vtp
# DESTINATION share
# COMPONENT OverView
# )

# Reset these so they don't cause problems building our subdirectories
SET(MOC_BUILT_SOURCES "")
SET(UI_BUILT_SOURCES "")
SET(RCS_SOURCES "")

ADD_SUBDIRECTORY(Utility)

# Generate OverView installers ...
#ADD_SUBDIRECTORY(Distribution)

